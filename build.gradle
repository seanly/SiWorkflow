buildscript {
    repositories {
        mavenCentral()
        maven { url 'http://repo.jenkins-ci.org/releases/' }
    }
    dependencies {
        classpath 'org.jenkins-ci.tools:gradle-jpi-plugin:0.18.1'
    }
}

apply plugin: 'java'
apply plugin: 'groovy'
apply plugin: 'idea'
apply plugin: 'org.jenkins-ci.jpi'
apply from: "$rootDir/dependency.gradle"

group 'xyz.lysean.jenkinsci'
description "Jenkins Job Pipeline View Jenkins plug-in"
version '1.0.0-SNAPSHOT'

plugins.withType(JavaPlugin) {
    sourceCompatibility = 1.6
    targetCompatibility = 1.6
}

tasks.withType(GroovyCompile) {
    groovyOptions.optimizationOptions.indy = true
}

repositories {
    mavenCentral()
    jcenter()
    maven { url 'http://repo.jenkins-ci.org/releases/' }
}

dependencies {
    compile libraries.'groovy',
            libraries.'guava',
            libraries.'slf4j',
            libraries.'logback'

    compile libraries.'snakeyaml'

    testCompile libraries.'spock-core',
            libraries.'cglib',
            libraries.'objenesis'
}


jenkinsPlugin {
    coreVersion = '1.609'
    displayName = 'Jenkins Job Pipeline View Jenkins plug-in'
    url = 'https://github.com/seanly/orchly-plugin/wiki'
    gitHubUrl = 'https://github.com/seanly/orchly-plugin'
    fileExtension = 'hpi'

    developers {
        developer {
            id 'seanly'
            name 'Sean Ly'
            email 'liouys@gmail.com'
        }
    }
}